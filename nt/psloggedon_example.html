<!doctype html><html lang="en">
<head><meta charset="UTF-8">
<link rel="STYLESHEET" href="../main.css" type="text/css">
<title>PsLoggedOn - example | Windows CMD | SS64.com</title>
</head><body><h1>List usernames and the computer each is logged into.</h1>
<p> (on a large domain this will take a while to run) </p>
<p>WhoWhere.cmd</p>
<pre>@echo off
setlocal
for /f "Tokens=1" %%c in ('net view /domain:"%USERDOMAIN%"^|Findstr /L /C:"\\"') do (
 for /f "Tokens=*" %%u in ('<a href="psloggedon.html">PsLoggedOn</a> -L %%c^|find /i "%USERDOMAIN%\"') do (
  call :report %%c "%%u"
 )
)
endlocal
goto :EOF

:report
set work=%1
set comp=%work:~2%
set user=%2
set user=%user:"=%
call set user=%%user:*%USERDOMAIN%\=%%
@echo %comp% %user%</pre></body>
</html>
